module guiconsole;

import java.awt.Component;
import java.awt.Dimension;
import java.awt.Graphics;
import java.awt.event.KeyEvent;
import java.awt.event.KeyListener;
import java.util.List;
import java.util.Map;
import java.util.concurrent.LinkedBlockingQueue;
import javax.swing.ImageIcon;
import javax.swing.JFrame;
import javax.swing.JPanel;

import yeti.lang.AList;

class KeyQueue extends KeyListener
	keys = new LinkedBlockingQueue(),
	
	number popKey() keys#take() unsafely_as number,
	
	void keyPressed(KeyEvent e) keys#put(e#getKeyCode()),
	void keyReleased(KeyEvent e),
	void keyTyped(KeyEvent e)
end;

class Pane(int w, int h, Map icons) extends JPanel
	var content = [""],
	
	drawIcon g x y icon is ~Graphics -> number -> number -> ~ImageIcon -> () =
		icon#paintIcon(() as ~Component, g, x * w, y * h),
	drawLine g y line is ~Graphics -> number -> string -> () = 
		for [0..strLength line - 1]
			(_ i = drawIcon g i y (icons#get(strChar line i) unsafely_as ~ImageIcon)),
	drawLines g y lines =
		if not empty? lines then drawLine g y (head lines);drawLines g (y+1) (tail lines) fi,
	
	void printContent(AList newContent)
		content := if nullptr? newContent then [""] else newContent unsafely_as list<string> fi;
		this#repaint(),
	
	void paintComponent(Graphics g)
		super#paintComponent(g);
		drawLines g 0 content,
end;

typedef t_console =
{
	title is string -> (),
	clear is () -> (),
	print is list?<string> -> (),
	getch is () -> number,
	close is () -> ()
};

loadIcon name is string -> ~ImageIcon = new ImageIcon(name);

console width height mapIcons is number -> number -> hash<string, string> -> t_console =
	(icons is hash<string, ~ImageIcon> = [:];
	 forHash mapIcons (_ k v = (icons.[k] := loadIcon v));
	 w = mapHash (_ k v = (v as ~Object unsafely_as ~ImageIcon)#getIconWidth()) icons |> fold max 0;
	 h = mapHash (_ k v = (v as ~Object unsafely_as ~ImageIcon)#getIconHeight()) icons |> fold max 0;

	 keyQueue = new KeyQueue();
	 frame = new JFrame();
	 frame#addKeyListener(keyQueue);
	 pane = new Pane(w, h, icons);
	 pane#addKeyListener(keyQueue);
	 pane#setPreferredSize(new Dimension(w * width, h * height));
	 frame#add(pane);
	 frame#setDefaultCloseOperation(JFrame#DISPOSE_ON_CLOSE);
	 frame#pack();
	 frame#setVisible(true);
	 {
		 title t = frame#setTitle(t),
		 clear () = pane#printContent([]),
		 print lines = pane#printContent(lines as ~Object unsafely_as ~AList),
		 getch () = number (string keyQueue#popKey()),
		 close () = frame#dispose()
	 });

{console}
